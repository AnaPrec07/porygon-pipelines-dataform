config {
  type: "table",
  schema: "walmart_targets_and_filters",
  description: "Stockout filter: filters out items with stockout events.",
  tags: ["intermediate", "filter"]
}



WITH percentiles AS (
  SELECT 
    tgt.ctx_state_id,
    tgt.ctx_store_id,
    tgt.ctx_cat_id,
    tgt.ctx_dept_id,
    APPROX_QUANTILES(tgt_monthly_sales, 100)[OFFSET(97)] AS outlier_threshold_q97,
  FROM ${ref("target_store_item_monthly")} tgt
  JOIN ${ref("stockout_filter")} stf
  ON stf.ctx_item_id = tgt.ctx_item_id
  AND stf.ctx_date_month = tgt.ctx_date_month
  AND stf.ctx_store_id = tgt.ctx_store_id
  WHERE is_incomplete_3m_tgt = 0
  GROUP BY    
    tgt.ctx_state_id,
    tgt.ctx_store_id,
    tgt.ctx_cat_id,
    tgt.ctx_dept_id
),
identify_outliers AS (
  SELECT 
    tgt.ctx_date_month,
    tgt.ctx_item_id,
    tgt.ctx_store_id,
    outlier_threshold_q97,
    CASE WHEN tgt_monthly_sales > outlier_threshold_q97 THEN 1 ELSE 0 END AS is_tgt_outlier,
  FROM ${ref("target_store_item_monthly")} tgt
  JOIN percentiles outl
    ON tgt.ctx_dept_id = outl.ctx_dept_id
    AND tgt.ctx_store_id = outl.ctx_store_id
)
SELECT 
  ctx_date_month,
  ctx_item_id,
  ctx_store_id,
  outlier_threshold_q97,
  is_tgt_outlier,
  MAX(is_tgt_outlier) OVER (
    PARTITION BY ctx_item_id, ctx_store_id 
    ORDER BY ctx_date_month 
    ROWS BETWEEN 1 FOLLOWING AND 3 FOLLOWING) AS is_outlier_3m_tgt,
FROM identify_outliers