config {
  type: "table",
  schema: "walmart_targets_and_filters",
  description: "Stockout filter: filters out items with stockout events.",
  tags: ["intermediate", "filter"]
}


WITH remove_zero_sales_days AS (
  SELECT 
  *
  FROM ${ref("daily_sales_melted")}
  WHERE daily_sales > 0
),
zero_sales_days_diff AS (
  SELECT 
    LAG(ctx_date, 1) OVER (PARTITION BY store_id, item_id ORDER BY ctx_date) AS ctx_last_day_sales,
    DATE_DIFF(ctx_date, LAG(ctx_date, 1) OVER (PARTITION BY store_id, item_id ORDER BY ctx_date), DAY) AS days_since_last_sale,
    *
  FROM remove_zero_sales_days
),
stockout_rate_threshold AS (
  SELECT 
    APPROX_QUANTILES(days_since_last_sale, 100)[OFFSET(90)] AS stockout_threshold_q90,
    item_id,
    store_id
  FROM zero_sales_days_diff
  GROUP BY 
    item_id,
    store_id
),
identify_stockouts AS (
  SELECT 
    DATE_TRUNC(ctx_date, MONTH) AS ctx_date_month,
    days_since_last_sale AS fea_days_since_last_sale,
    item_id AS ctx_item_id,
    days.store_id AS ctx_store_id,
    CASE 
      WHEN 
        days_since_last_sale >= stockout_threshold_q90 
      THEN 1 
      ELSE 0 
    END AS is_stockout,
  FROM zero_sales_days_diff days
  JOIN stockout_rate_threshold stckout
    ON days.item_id = stckout.item_id
    AND days.store_id = stckout.store_id
),
aggregate_daily_to_monthly AS (
  SELECT 
    MAX(is_stockout) AS is_stockout,
    MAX(fea_days_since_last_sale) AS fea_max_days_since_last_sale,
    ctx_date_month,
    ctx_item_id, 
    ctx_store_id
  FROM identify_stockouts
  GROUP BY  
    ctx_date_month, 
    ctx_item_id, 
    ctx_store_id
)
SELECT *,
LEAD(is_stockout, 1) OVER (PARTITION BY ctx_item_id, ctx_store_id ORDER BY ctx_date_month) AS is_stockout_lead_1,
LEAD(is_stockout, 2) OVER (PARTITION BY ctx_item_id, ctx_store_id ORDER BY ctx_date_month) AS is_stockout_lead_2,
LEAD(is_stockout, 3) OVER (PARTITION BY ctx_item_id, ctx_store_id ORDER BY ctx_date_month) AS is_stockout_lead_3
FROM aggregate_daily_to_monthly 

