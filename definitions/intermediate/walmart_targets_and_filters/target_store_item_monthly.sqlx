config {
  type: "table",
  schema: "walmart_targets_and_filters",
  description: "Target for deptartment, item, daily hierarchy.",
  columns: {
    ctx_date_month: walmart_descriptions.ctx_date_month,
    ctx_state_id: walmart_descriptions.ctx_state_id,
    ctx_store_id: walmart_descriptions.ctx_store_id,
    ctx_cat_id: walmart_descriptions.ctx_cat_id,
    ctx_dept_id: walmart_descriptions.ctx_dept_id,
    ctx_item_id: walmart_descriptions.ctx_item_id,
    tgt_monthly_sales: walmart_descriptions.tgt_monthly_sales,
    tgt_monthly_sales_lead_1_months: walmart_descriptions.tgt_monthly_sales_lead_1_months,
    tgt_monthly_sales_lead_2_months: walmart_descriptions.tgt_monthly_sales_lead_2_months,
    tgt_monthly_sales_lead_3_months: walmart_descriptions.tgt_monthly_sales_lead_3_months,
    tgt_monthly_sales_lead_4_months: walmart_descriptions.tgt_monthly_sales_lead_4_months,
  },
  tags: ["intermediate"],
  assertions: {
    nonNull: [
        "ctx_date_month",
        "ctx_state_id",
        "ctx_store_id",
        "ctx_cat_id",
        "ctx_dept_id",
        "ctx_item_id",
        "tgt_monthly_sales",
    ],
    uniqueKey: [
        "ctx_date_month",
        "ctx_state_id",
        "ctx_store_id",
        "ctx_cat_id",
        "ctx_dept_id",
        "ctx_item_id",
    ],
  }
}

WITH dept_item_daily_sales AS (
  SELECT
    day,
    SUM(sales) AS daily_sales,
    state_id,
    store_id,
    cat_id,
    dept_id,
    item_id
  FROM (
    SELECT *
    FROM ${ref("sales_train_validation")}
    UNPIVOT (
      sales FOR day IN (
            ${schema_helpers.selectDayColumns(1, 1913, 'd_')}
        )
    )
  )
  GROUP BY 
    day,
    state_id,
    store_id,
    cat_id,
    dept_id,
    item_id
),
dept_item_daily_sales_with_calendar_dates AS (
    SELECT 
        cal.date AS ctx_date,
        daily_sales.state_id AS ctx_state_id,
        daily_sales.store_id AS ctx_store_id,
        daily_sales.cat_id AS ctx_cat_id,
        daily_sales.dept_id AS ctx_dept_id,
        daily_sales.item_id AS ctx_item_id,
        daily_sales.daily_sales AS tgt_daily_sales,
        CASE WHEN daily_sales.daily_sales = 0 THEN 1 ELSE 0 END AS tgt_is_zero_sales
    FROM dept_item_daily_sales daily_sales
    JOIN ${ref("calendar")} cal
    ON cal.d = daily_sales.day
),
dept_item_monthly_sales AS (
    SELECT 
        DATE_TRUNC(ctx_date, MONTH) AS ctx_date_month,
        ctx_state_id,
        ctx_store_id,
        ctx_cat_id,
        ctx_dept_id,
        ctx_item_id,
        SUM(tgt_daily_sales) AS tgt_monthly_sales,
        SUM(tgt_is_zero_sales) AS tgt_days_is_zero_sales
    FROM dept_item_daily_sales_with_calendar_dates
    GROUP BY 
        DATE_TRUNC(ctx_date, MONTH),
        ctx_state_id,
        ctx_store_id,
        ctx_cat_id,
        ctx_dept_id,
        ctx_item_id
)
SELECT 
    ctx_date_month,
    ctx_state_id,
    ctx_store_id,
    ctx_cat_id,
    ctx_dept_id,
    ctx_item_id,
    tgt_monthly_sales,
    tgt_days_is_zero_sales,
    SUM(tgt_monthly_sales) OVER (
      PARTITION BY ctx_state_id, ctx_store_id, ctx_cat_id, ctx_dept_id, ctx_item_id 
      ORDER BY ctx_date_month
      ROWS BETWEEN 1 FOLLOWING AND 3 FOLLOWING
    ) AS tgt_monthly_sales_sum_3_next_months,
    ${window_functions.generateLeadColumns(
    "tgt_monthly_sales", 
    4, 
    "ctx_state_id, ctx_store_id, ctx_cat_id, ctx_dept_id, ctx_item_id",
    "ctx_date_month"
    )},
    ${window_functions.generateLeadColumns(
    "tgt_days_is_zero_sales", 
    4, 
    "ctx_state_id, ctx_store_id, ctx_cat_id, ctx_dept_id, ctx_item_id",
    "ctx_date_month"
    )}
FROM dept_item_monthly_sales