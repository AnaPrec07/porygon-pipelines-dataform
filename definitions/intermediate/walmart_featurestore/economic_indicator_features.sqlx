config {
  type: "table",
  schema: "walmart_featurestore",
  description: "Rolling economic indicator features.",
  tags: ["intermediate"],
  assertions: {
    nonNull: [
      "ctx_date_month"
    ],
    uniqueKey: [
      "ctx_date_month"
    ],
  }
}

WITH economic_indicators AS (
    SELECT 
        PARSE_DATE('%m/%d/%y', observation_date) AS ctx_date_month,
        fea_wi_unemployment_rate,
        fea_tx_unemployment_rate,
        fea_ca_unemployment_rate,
        fea_gas_price_per_gallon,
    FROM ${ref("economic_features")}
)
SELECT 
    ctx_date_month,
    fea_wi_unemployment_rate,
    fea_tx_unemployment_rate,
    fea_ca_unemployment_rate,
    fea_gas_price_per_gallon,
    ${window_functions.generateRollMedianSTDEVNoPartition("fea_wi_unemployment_rate")},
    ${window_functions.generateRollMedianSTDEVNoPartition("fea_tx_unemployment_rate")},
    ${window_functions.generateRollMedianSTDEVNoPartition("fea_ca_unemployment_rate")},
    ${window_functions.generateRollMedianSTDEVNoPartition("fea_gas_price_per_gallon")},
FROM economic_indicators