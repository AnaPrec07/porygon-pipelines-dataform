config {
  type: "table",
  schema: "walmart_featurestore",
  name: "autocorrelation_features",
  description: "Autocorrelation features for item/store/month.",
  tags: ["intermediate", "walmart", "features"],
  assertions: {
    nonNull: [
      "ctx_date_month",
      "ctx_item_id",
      "ctx_store_id",
    ],
    uniqueKey: [
      "ctx_date_month",
      "ctx_item_id",
      "ctx_store_id",
    ],
  }
}

WITH ts AS (
  SELECT
    ctx_item_id,
    ctx_store_id,
    ctx_date_month,
    fea_item_monthly_sales AS x,
    fea_item_monthly_sales_lag_1_months AS lag1,
    fea_item_monthly_sales_lag_2_months AS lag2,
    fea_item_monthly_sales_lag_3_months AS lag3,
    fea_item_monthly_sales_lag_4_months AS lag4,
  FROM `porygon-pipelines.walmart_featurestore.rolling_sales_features`
),
rolling AS (
  SELECT
    ctx_item_id,
    ctx_store_id,
    ctx_date_month,

    -- n = number of valid rows in the window
    COUNT(lag1) OVER w AS n_lag1,
    COUNT(lag2) OVER w AS n_lag2,
    COUNT(lag3) OVER w AS n_lag3,
    COUNT(lag4) OVER w AS n_lag4,

    -- sums lag 1
    SUM(x) OVER w AS sum_x,
    SUM(lag1) OVER w AS sum_lag1,
    SUM(x * lag1) OVER w AS sum_xlag1,
    SUM(x * x) OVER w AS sum_x2,
    SUM(lag1 * lag1) OVER w AS sum_lag1_2,

    -- sums lag 2
    SUM(lag2) OVER w AS sum_lag2,
    SUM(x * lag2) OVER w AS sum_xlag2,
    SUM(lag2 * lag2) OVER w AS sum_lag2_2,

    -- sums lag 3
    SUM(lag3) OVER w AS sum_lag3,
    SUM(x * lag3) OVER w AS sum_xlag3,
    SUM(lag3 * lag3) OVER w AS sum_lag3_2,

    -- sums lag 4
    SUM(lag4) OVER w AS sum_lag4,
    SUM(x * lag4) OVER w AS sum_xlag4,
    SUM(lag4 * lag4) OVER w AS sum_lag4_2

  FROM ts
  WINDOW w AS (
    PARTITION BY ctx_item_id, ctx_store_id
    ORDER BY ctx_date_month
    ROWS BETWEEN 11 PRECEDING AND CURRENT ROW
  )
)
SELECT
  ctx_item_id,
  ctx_store_id,
  ctx_date_month,
  SAFE_DIVIDE(
    (sum_xlag1* n_lag1 - (sum_x * sum_lag1)),
    SQRT(
      ABS(((n_lag1*sum_x2)-POWER(sum_x,2)) *
      ((n_lag1*sum_lag1_2)-POWER(sum_lag1,2)))
    )
  ) AS fea_autocorrelation_lag1,
  SAFE_DIVIDE(
    (sum_xlag2* n_lag2 - (sum_x * sum_lag2)),
    SQRT(
      ABS(((n_lag2*sum_x2)-POWER(sum_x,2)) *
      ((n_lag2*sum_lag2_2)-POWER(sum_lag2,2)))
    )
  ) AS fea_autocorrelation_lag2,
  SAFE_DIVIDE(
    (sum_xlag3* n_lag3 - (sum_x * sum_lag3)),
    SQRT(
      ABS(((n_lag3*sum_x2)-POWER(sum_x,2)) *
      ((n_lag3*sum_lag3_2)-POWER(sum_lag3,2)))
    )
  ) AS fea_autocorrelation_lag3,
  SAFE_DIVIDE(
    (sum_xlag4* n_lag4 - (sum_x * sum_lag4)),
    SQRT(
      ABS(((n_lag4*sum_x2)-POWER(sum_x,2)) *
      ((n_lag4*sum_lag4_2)-POWER(sum_lag4,2)))
    )
  ) AS fea_autocorrelation_lag4
FROM rolling
ORDER BY ctx_item_id, ctx_store_id, ctx_date_month
