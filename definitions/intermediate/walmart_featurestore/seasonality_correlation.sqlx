config {
  type: "table",
  schema: "walmart_featurestore",
  name: "seasonality_correlation",
  description: "Seasonality correlation features for item/store/month.",
  tags: ["intermediate", "walmart", "features"],
  assertions: {
    nonNull: [
      "ctx_date_month",
      "ctx_item_id",
      "ctx_store_id",
    ],
    uniqueKey: [
      "ctx_date_month",
      "ctx_item_id",
      "ctx_store_id",
    ],
  }
}

WITH ts AS (
  SELECT
    m.ctx_item_id,
    m.ctx_store_id,
    m.ctx_date_month,
    tgt_monthly_sales_sum_3_next_months AS x,
    ROUND(fea_month_sin, 2)+1 AS msev,
    tgt_monthly_sales_sum_3_next_months,
    is_stockout_tgt,
  FROM `porygon-pipelines.walmart_training_tables.walmart_master_table` m
  LEFT JOIN `porygon-pipelines.walmart_targets_and_filters.stockout_filter` stf
      ON m.ctx_item_id = stf.ctx_item_id
      AND m.ctx_date_month = stf.ctx_date_month
      AND m.ctx_store_id = stf.ctx_store_id
  WHERE fea_item_monthly_sales > 0
  AND is_stockout_tgt = 0
),
rolling AS (
  SELECT
    ctx_item_id,
    ctx_store_id,
    ctx_date_month,
    -- features
    x,
    SUM(x) OVER w AS sum_x,
    SUM(x * x) OVER w AS sum_x2,
    -- msev features
    msev,
    COUNT(msev) OVER w AS n_msev,
    SUM(msev) OVER w AS sum_msev,
    SUM(x * msev) OVER w AS sum_xmsev,
    SUM(msev * msev) OVER w AS sum_msev_2,
    tgt_monthly_sales_sum_3_next_months,
    is_stockout_tgt,
  FROM ts
  WINDOW w AS (
    PARTITION BY ctx_item_id, ctx_store_id
    ORDER BY ctx_date_month
    ROWS BETWEEN UNBOUNDED PRECEDING AND CURRENT ROW
  )
),
correlations_calculations AS (
SELECT
  ctx_item_id,
  ctx_store_id,
  ctx_date_month,
  SAFE_DIVIDE(
    (sum_xmsev* n_msev - (sum_x * sum_msev)),
    SQRT(
      ((n_msev*sum_x2)-POWER(sum_x,2)) *
      ((n_msev*sum_msev_2)-POWER(sum_msev,2))
    )
  ) AS fea_seasonality_correlation,
  msev
FROM rolling
), 
shift_correlations AS (
  SELECT 
    ctx_item_id,
    ctx_store_id,
    ctx_date_month,
    msev,
    LAG(fea_seasonality_correlation, 3) OVER (ORDER BY ctx_date_month) AS fea_seasonality_correlation_lag3,
  FROM correlations_calculations
)
SELECT 
  ctx_item_id,
  ctx_store_id,
  ctx_date_month,
  fea_seasonality_correlation_lag3,
  CASE 
    WHEN ABS(fea_seasonality_correlation_lag3) >0.6
    THEN SIGN(fea_seasonality_correlation_lag3) * msev 
    ELSE NULL 
  END AS fea_seasonality_if_correlated
FROM shift_correlations
