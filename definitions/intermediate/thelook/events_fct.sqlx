config {
  type: "table",
  schema: "thelook",
  description: "Cleaned version of raw events table",
  columns: {
    event_id: docs.event_id,
    user_id: docs.user_id,
    sequence_number: docs.sequence_number,
    session_id: docs.session_id,
    date_created: docs.date_created,
    traffic_source: docs.traffic_source,
    uri: docs.uri,
    event_type: docs.event_type,
    processing_timestamp: docs.processing_timestamp
  },
  tags: ["intermediate"],
  assertions: {
    nonNull: ["event_id"],
    uniqueKey: ["event_id"],
  }
}

select
  id as event_id,
  user_id,
  sequence_number,
  session_id,
  date_created, 
  traffic_source,
  uri,
  event_type,
  ${date_functions.getRuntimeTimestamp()}
from
  ${ref("events")}