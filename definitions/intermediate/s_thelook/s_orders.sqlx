config {
  type: "table",
  schema: "s_thelook",
  description: "Cleaned version of raw orders table",
  columns: {
    order_id: docs.order_id,
    user_id: docs.user_id,
    status: docs.status,
    created_at: docs.created_at,
    returned_at: docs.returned_at,
    shipped_at: docs.shipped_at,
    delivered_at: docs.delivered_at,
    num_of_items: docs.num_of_items,
    order_processing_time: docs.order_processing_time,
    order_transit_time: docs.order_transit_time,
    order_lead_time: docs.order_lead_time,
    processed_at: docs.processed_at,
  },
  tags: ["staging"],
  assertions: {
    nonNull: ["order_id"],
    uniqueKey: ["order_id"],
    rowConditions: [assertions.table_successfully_updated]
  }
}

SELECT 
  order_id,
  user_id,
  status,
  created_at,
  returned_at,
  shipped_at,
  delivered_at,
  num_of_items,
  shipped_at-created_at AS order_processing_time,
  delivered_at-shipped_at AS order_transit_time,
  delivered_at-created_at AS order_lead_time,
  ${date_functions.getRuntimeTimestamp()}
FROM ${ref("orders")}